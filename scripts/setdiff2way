#!/usr/bin/awk -E
#
# USAGE:
# setdiff2way file1 file2
#
# DESCRIPTION:
# Prints lines in file1 that are not in file2, and then the revserse.
BEGIN {
    # If two arguments are not given, print usage and exit. The program
    # name is counted as first argument.
    if (ARGC != 3) {
        print "Usage: setdiff2way file1 file2"
        exit 1
    }
    file_num = 0
}

FNR == 1 { file_num++ }

{
    if (file_num == 1) {
        file_one[$0]
    }
    else if (file_num == 2) {
        file_two[$0]
    }
}

END {
    for (line in file_one) {
        if (!(line in file_two)) {
            one_minus_two[line]
            one_minus_two_count++
        }
    }

    if (one_minus_two_count > 0) {
        printf "A - B (%s)\n", one_minus_two_count
        for (line in one_minus_two) {
            print line | "sort -V"
        }

        close("sort -V")
    }

    for (line in file_two) {
        if (!(line in file_one)) {
            two_minus_one[line]
            two_minus_one_count++
        }
    }

    if (two_minus_one_count > 0) {
        printf "B - A (%s)\n", two_minus_one_count
        for (line in two_minus_one) {
            print line | "sort -V"
        }
        close("sort -V")
    }
}
