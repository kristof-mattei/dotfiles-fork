#!/usr/bin/awk -E
#
# USAGE:
# setdiff2way [options]... file1 file2
#
# OPTIONS:
#  -1 n  set column to extract from file1 (default: 1)
#  -2 n  set column to extract from file2 (default: 1)
#
# DESCRIPTION:
# Prints lines in file1 that are not in file2, and then the revserse.
BEGIN {
    # If two arguments are not given, print usage and exit. The program
    # name is counted as first argument.

    FS="\t"

    col_file_1 = 1
    col_file_2 = 1

    name_file_1 = "A"
    name_file_2 = "B"

    print_shared = 0

    start_row_file_1 = 1
    start_row_file_2 = 1

    for (i = 1; i < ARGC; i++) {
        if (ARGV[i] == "-h" || ARGV[i] == "--help") {
            print "Usage: setdiff2way [options]... file1 file2"
            print "Options:"
            print "  (-1 | --1col) n  set column to extract from file1 (default: 1)"
            print "  (-2 | --2col) n  set column to extract from file2 (default: 1)"
            print "  --1name name     set name for file 1 (default: A)"
            print "  --2name name     set name for file 2 (default: B)"
            print "  -s               print shared lines"
            print "  --h1             skip header line in file 1"
            print "  --h2             skip header line in file 2"
            exit 0
        }
        else if (ARGV[i] == "--1col" || ARGV[i] == "-1") {
            col_file_1 = ARGV[i + 1]
            ARGV[i] = ""
            ARGV[i + 1] = ""
            i++
        } else if (ARGV[i] == "--2col" || ARGV[i] == "-2") {
            col_file_2 = ARGV[i + 1]
            ARGV[i] = ""
            ARGV[i + 1] = ""
            i++
        }
        else if (ARGV[i] == "--1name") {
            name_file_1 = ARGV[i + 1]
            ARGV[i] = ""
            ARGV[i + 1] = ""
            i++
        }
        else if (ARGV[i] == "--2name") {
            name_file_2 = ARGV[i + 1]
            ARGV[i] = ""
            ARGV[i + 1] = ""
            i++
        }
        else if (ARGV[i] == "--shared") {
            print_shared = 1
            ARGV[i] = ""
        }
        else if (ARGV[i] == "--h1") {
            start_row_file_1 = 2
            ARGV[i] = ""
        }
        else if (ARGV[i] == "--h2") {
            start_row_file_2 = 2
            ARGV[i] = ""
        }
    }

    file_num = 0
}

FNR == 1 { file_num++ }

{
    if (file_num == 1 && FNR >= start_row_file_1) {
        file_one[$col_file_1]
    }
    else if (file_num == 2 && FNR >= start_row_file_2) {
        file_two[$col_file_2]
    }
}

END {
    for (line in file_one) {
        if (!(line in file_two)) {
            one_minus_two[line]
            one_minus_two_count++
        }
    }

    if (one_minus_two_count > 0) {
        printf "%s - %s (%s)\n", name_file_1, name_file_2, one_minus_two_count
        for (line in one_minus_two) {
            print line | "sort -V"
        }

        close("sort -V")
    }

    for (line in file_two) {
        if (!(line in file_one)) {
            two_minus_one[line]
            two_minus_one_count++
        }
    }

    if (two_minus_one_count > 0) {
        printf "%s - %s (%s)\n", name_file_2, name_file_1, two_minus_one_count
        for (line in two_minus_one) {
            print line | "sort -V"
        }
        close("sort -V")
    }

    if (print_shared) {
        for (item in file_one) {
            if (item in file_two) {
                shared[item]
                shared_count++
            }
        }

        if (shared_count > 0) {
            printf "%s & %s (%s)\n", name_file_1, name_file_2, shared_count
            for (item in shared) {
                print item | "sort -V"
            }
            close("sort -V")
        }

    }
}
